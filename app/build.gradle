apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'

/*
 * Copyright 2020 Clifford Liu
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// below: load the desired values from custom.properties in order to be injected into BuildConfig and Res
// the values in custom.properties must not contain quotes otherwise the parsed values here will contain quotes
Properties properties = new Properties()
properties.load(project.rootProject.file("custom.properties").newDataInputStream())
def buildPackage = properties.getProperty("packageName", "")
def signingKeyStorePath = properties.getProperty("signingKeyStorePath", "")
def signingKeyStorePassword = properties.getProperty("signingKeyStorePassword", "")
def signingKeyAlias = properties.getProperty("signingKeyAlias", "")
def signingKeyPassword = properties.getProperty("signingKeyPassword", "")

dokka {
    outputFormat = 'html'
    outputDirectory = "./kdoc"
}

android {
    buildToolsVersion '29.0.3'
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    signingConfigs {
        Sign4Release {
            keyAlias signingKeyAlias
            keyPassword signingKeyPassword
            storeFile file(signingKeyStorePath)
            storePassword signingKeyStorePassword
        }
    }
    compileSdkVersion 29
    defaultConfig {
        // below: manifest placeholders
        manifestPlaceholders = [buildPackage: buildPackage]
        applicationId "com.madness.collision"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 20050313
        versionName '3.3'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId "$applicationId\\.test"
        renderscriptSupportModeEnabled true
        signingConfig signingConfigs.Sign4Release
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
        // below: inject the desired values into BuildConfig and Res
        // the string values have to be wrapped in quotes because the value in local.properties does not have quotes
        buildConfigField "String", "BUILD_PACKAGE", "\"$buildPackage\""
        resValue "string", "buildPackage", buildPackage
        // below: fix multi-locale support
        resConfigs "en", "en-rGB", "en-rUS", "zh", "zh-rCN", "ru", "ru-rRU", "es", "es-rES", "es-rUS", "ar", "it", "it-rIT", "pt", "pt-rPT", "th", "th-rTH", "vi", "vi-rVN", "fr", "fr-rFR"
    }
    flavorDimensions "arch"
    productFlavors {
        full {
            dimension "arch"
        }
        arm {
            dimension "arch"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
        x86 {
            dimension "arch"
            ndk {
                abiFilters "x86", "x86_64"
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.mortal'
            debuggable true
            jniDebuggable false
            signingConfig signingConfigs.Sign4Release
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.Sign4Release
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    dexOptions {
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    dataBinding.enabled = true
    viewBinding.enabled = true
    dynamicFeatures = [":cool_app", ":image_modifying", ":themed_wallpaper", ":school_timetable", ":no_media", ":api_viewing", ":audio_timer", ":we_chat_evo", ":qq_contacts"]
}

def kotlin_version = "1.3.72"
def nav_version = "2.2.2"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.4'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.0-beta01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

//    def futures_version = "1.0.0-alpha02"
//    implementation "androidx.concurrent:concurrent-futures:$futures_version"
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    implementation "androidx.paging:paging-runtime-ktx:2.1.2"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    //androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

//    def work_version = "2.0.1"
//    implementation "androidx.work:work-runtime-ktx:$work_version"
//    androidTestImplementation "androidx.work:work-testing:$work_version"
    // optional - Firebase JobDispatcher support
    //implementation "androidx.work:work-firebase:$work_version"

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    def truth_ver = "1.0"
    androidTestImplementation "com.google.truth:truth:$truth_ver"
    testImplementation "com.google.truth:truth:$truth_ver"
    testImplementation "com.google.truth.extensions:truth-java8-extension:$truth_ver"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
    implementation 'com.google.android.play:core:1.7.2'
}
